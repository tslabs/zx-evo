
;LAST UPDATE: 06.03.2011 savelij

SELECTOR	XOR A
		OUT (PEVO_CONF),A
		CALL DETECTMOUSE
		EI	
		LD A,(FLAGS)
		AND 2				;  
		JR Z,_RULNMO
;FIX  
MKEYPR		EI
		HALT
		LD A,0XFA
		IN A,(0XDF)
		CPL
		AND 7
		JR NZ,MKEYPR
_RULNMO		CALL SAVE2X2			;    
		CALL SET_ADR_ATR		;     
		LD HL,FLAGS_KEY
		RES 5,(HL)			;    
		JR MAINLOP

;  
UP		CALL CURSOR_UP			;    -1
		JR SET_POS1			

;  
RIGHT		BIT 1,(IX+6)			;    
		PUSH AF				;  
		CALL NZ,PAGEDN			; ,   
		POP AF				;  
		JR NZ,SET_POS1			;  
		LD A,(IX+0X0A)
		AND A
		JR Z,SET_POS
		DEC A				;     
		JR SET_POS			;    

;  
DOWN		CALL CURSOR_DOWN		;    +1
		JR SET_POS1

;  
LEFT		BIT 1,(IX+6)			;    
		PUSH AF				;  
		CALL NZ,PAGEUP			; ,   
		POP AF				;  
		JR NZ,SET_POS1			;  
		XOR A				;     
SET_POS		BIT 7,(IX+7)
		JR Z,SET_POS3
		LD L,(IX+8)
		LD H,(IX+9)
		LD (HL),A
		INC HL
		LD (HL),A
		INC HL
		LD (HL),0
		JR SET_POS1

SET_POS3	LD (IX+7),A			;  
		LD (IX+8),A
		LD (IX+9),0			;   
SET_POS1	CALL COLOR_CURSOR		;   
SET_POS2	CALL GLUDIN			;  AY
MAINLOP		LD HL,FLAGS_KEY
		RES 5,(HL)			;  
		LD A,(FLAGS)
		AND 2				;  
		JP Z,MAINNMO
		LD HL,(ARXY)			;  
		PUSH HL
		CALL MOUSE			;  
		POP BC
		AND A
		SBC HL,BC
		JR Z,NO_SELECT			;   
		CALL RESTORE_KOSHAK		;  ,   
		CALL MOUOPT			;     
NO_SELECT	LD BC,0XFADF
		IN A,(C)			;   
		AND 7
		CP 6
		JR Z,PRESS_MOUSE		;    
		CP 5
		JP Z,RESTART			;    
		CALL PRINTTIME			;   
		EI
		HALT
		CALL REST2X2			;    
		CALL DRAW_MOUSE			;  
		JR MAINQMO			; 

PRESS_MOUSE	CALL OPMSPL
		AND A
		JR Z,CP_MOUSE4
		LD (LAST_K),A
		CALL TIMELP
		JR SELECT_KEY

CP_MOUSE4	CALL MOUOPT			;      
		JR C,MAINNMO			;    
		LD E,(IX+7)			;     
		BIT 7,E
		JR Z,CP_MOUSE5
		LD L,(IX+8)
		LD H,(IX+9)
		LD E,(HL)
CP_MOUSE5	LD D,0
		LD HL,(PRESSEDKEY)		;    
		ADD HL,DE			;   
		LD DE,LAST_K
		LDI				;   
		JR ENTER
		
MAINNMO		CALL PRINTTIME			; ,    
		EI
		HALT
MAINQMO		LD A,(FLAGS_KEY)
		BIT 5,A				;   
		CALL Z,CP_TIME_KOSHAK		;   ,   
		JP Z,MAINLOP			;  
		CALL BREAK_KEY			;  BREAK
		JR C,SELECT_KEY			; BREAK  
RESTART		LD A,1
		OUT (PEVO_CONF),A
		RET

SELECT_KEY 	CALL RESTORE_KOSHAK		;   ,   
		LD HL,LAST_K
		LD A,(HL)			;  
		LD B,0
		LD HL,MAIN_KEYS
		LD C,(HL)			;  
		LD D,C
		INC HL
		CPIR
		JR NZ,NOMAINKEYS		;  ,     
						; -   ,  
		LD HL,ADREXEKEYS
		LD A,D				;    -1
		SUB C				;  
		DEC A
		ADD A,A
		ADD A,L
		LD L,A
		ADC A,H
		SUB L
		LD H,A
		LD A,(HL)
		INC HL
		LD H,(HL)
		LD L,A
		JP (HL)

;    
NOMAINKEYS	LD HL,(PRESSEDKEY)		;     
		LD C,A
		LD A,H
		OR L
		JP Z,MAINLOP			;  ,  
		LD A,C
		LD C,(IX+2)
		LD E,(HL)
		INC HL
		DEC C				; 
		DEC C				;  -2
		LD D,C
		LD B,0
		CPIR				;   
		JR NZ,OSTAT_KEYS
		LD A,D				; 
		SUB C
		DEC A				;     -1
		BIT 7,(IX+7)
		JR Z,ENTER1
		LD L,(IX+8)
		LD H,(IX+9)
		LD (HL),A
		INC HL
		LD (HL),A
		JR ENTER

ENTER1		LD (IX+7),A			;   
		LD (IX+8),A			;     
ENTER		LD A,(FLAGS)
		AND 2
		CALL NZ,TIMELP			;    	
		CALL REST2X2			;  
		CALL GLUDIN			;  AY
		LD A,(IX+7)			;    
		BIT 7,A
		JR Z,JUMP2HL1
		LD L,(IX+8)
		LD H,(IX+9)
		LD A,(HL)
JUMP2HL1	LD L,(IX+0X0E)
		LD H,(IX+0X0F)			;   1    
JUMP2HL		ADD A,A
		ADD A,L
		LD L,A
		ADC A,H
		SUB L
		LD H,A
		LD A,(HL)
		INC HL
		LD H,(HL)
		LD L,A
		LD A,1
		OUT (PEVO_CONF),A
		JP (HL)

OSTAT_KEYS	EX AF,AF'
		LD A,E
		SUB D
		JP Z,MAINLOP
		LD C,A
		EX AF,AF'
		CPIR
		JP NZ,MAINLOP
		LD A,E
		SUB C
		DEC A
		JR JUMP2HL1

TIMELP		CALL PRINTTIME
		LD A,0XFA
		IN A,(0XDF)			; 
		CPL
		AND 7
		JR NZ,TIMELP			;  
		RET

GLUDIN		LD HL,DIN+0X0D
		LD A,0X0D
GLUDIN1		LD BC,0XFFFD
		OUT (C),A
		LD B,0XBF
		OUTD
		SUB 1
		JR NC,GLUDIN1
		RET

KOSHAK		BINCLUDE kot_anim.bin,9
