
;LAST UPDATE: 03.08.2011 savelij

NMI_SAVE_SP	EQU 0X00BE
RST8_SAVE_SP	EQU 0X00FD
IM_WORK		EQU 0X00FF

RAM_SEL
		PHASE ADRNMIRST8
		OUT (C),A			;   
FOR_RET		LD (RST8_SAVE_SP),SP
		LD SP,RST8_SAVE_SP-0X0A
		PUSH DE
		EXX
		PUSH BC
		PUSH DE
		PUSH HL
		EXX
		PUSH IX
		PUSH IY
		EX AF,AF'
		PUSH AF
		LD (DNO_SAVE_SP),SP
		LD HL,(RST8_SAVE_SP)
		LD DE,RST8_SAVE_SP-0X0A
		LD BC,0X0A
		JR RAMSEL1

		DUPL 0X0038-$,0
IM_EI_RET	EI
		RET

RAMSEL1		LDIR
		LD A,0XC9
		LD (FOR_RET),A
		LD BC,WIN_P2
		LD HL,0X8000
		OUT (C),L
		ADD HL,SP
		LD SP,HL
		LD HL,8
		LD B,HIGH (WIN_A0)
		LD A,5
		PUSH HL
		JR 0X0013

OUT_NMI		OUT (0XBE),A
		RETN

		DUPL 0X0066-$,0XFF
		JP NMI_SERVICE

		JP EXIT_RST8

		JP EXITNMISERVICE

B_BF		DB 0
B_77		DB 0
B_EFF7		DB 0
B_7FFD		DB 0
B_DOS7FFD	DB 0
B_RAMNROM	DB 0
B_1WINA3	DB 0
B_1WINA2	DB 0
B_1WINA1	DB 0
B_1WINA0	DB 0
B_0WINA3	DB 0
B_0WINA2	DB 0
B_0WINA1	DB 0
B_0WINA0	DB 0
TEK_PALS	DUPL 0X10,0

		DEPHASE
ERAM_SEL

		PHASE 0X0101
EXIT_RST8	LD A,0XED
		LD (FOR_RET),A
		LD BC,WIN_P2
		LD A,0XFD
		OUT (C),A
		LD HL,RST8_SAVE_SP-0X0A
		LD DE,(RST8_SAVE_SP)
		LD BC,0X0A
		LDIR
		LD SP,0
DNO_SAVE_SP	EQU $-2
		POP AF
		EX AF,AF'
		POP IY
		POP IX
		POP HL
		POP DE
		POP BC
		EXX
		POP DE
		LD SP,(RST8_SAVE_SP)
		LD BC,0X3CC8			;    48
		PUSH BC
		LD BC,WIN_A0
		LD A,0X83
		JP 0X0013

;HL AF IF RF DE BC HL' DE' BC' IX IY AF' 
NMI_SERVICE	LD (NMI_SAVE_SP),SP		; 
		LD SP,NMI_SAVE_SP		; 
		PUSH HL				; HL
		PUSH AF				; AF
		LD A,I
		PUSH AF				; I,     
		LD A,R
		PUSH AF				; R
		PUSH DE				; DE
		PUSH BC				; BC
		EXX
		PUSH HL				;  HL
		PUSH DE				;  DE
		PUSH BC				;  BC
		PUSH IX				; IX
		PUSH IY				; IY
		EX AF,AF'
		PUSH AF				;  AF
		LD A,0XC9
		LD (FOR_RET),A			;RET    ROM/RAM
		LD (RET_SP),SP			;     
		LD HL,IM_EI_RET			;  INT
		LD (IM_WORK),HL
		LD HL,0X0066
		LD (HL),0XC9			;  MAGIC
		LD HL,B_BF			;    
		IN A,(PEVO_CONF)
		LD (HL),A
		INC HL
		LD A,1
		OUT (PEVO_CONF),A
		LD BC,0X0CBE
NMISERV002	INIR
		INI
		XOR A
		LD I,A				;     00FF
		LD DE,0XAE0F			;   
		LD BC,0XBD77
;		OUT (C),D			; 
		LD BC,0XDBE
		EI
		HALT
		DI
NMISERV003	LD A,E
		BIT 3,E
		OUT (0XFE),A
		JR Z,NMISERV004
		OUT (0XF6),A
NMISERV004	IN A,(C)
		AND 0XF3
		LD (HL),A
		INC HL
		DEC E
		JP P,NMISERV003			; 
;		LD BC,0X7FFD
;		LD A,(B_7FFD)
;		OR 8
;		OUT (C),A
;		LD BC,WIN_P1
;		LD A,3!0XFF
;		OUT (C),A			;   
;		LD B,HIGH (WIN_A3)
;		LD A,0X7F
;		OUT (C),A
;		LD B,HIGH (WIN_P3)
;		LD A,7!0XFF
;		OUT (C),A			;   
		LD BC,WIN_P2
		LD HL,0X8000
		OUT (C),L			;   
		ADD HL,SP
		LD SP,HL
		LD HL,0
		PUSH HL
		LD B,HIGH (WIN_A0)
		LD A,5
		OUT (C),A
		JP OUT_NMI

;  
EXITNMISERVICE	IN A,(PEVO_CONF)
		SET 3,A
		OUT (PEVO_CONF),A
		RES 3,A
		OUT (PEVO_CONF),A
		HALT				;  MAGIC 
FALSE_NMI	LD HL,0X0066
		LD (HL),0XC3			;  MFGIC  
		LD BC,WIN_A0
		LD DE,(B_DOS7FFD)		;D=B_RAMNROM, E=B_DOS7FFD
		REPT 4
		SRL D
		SRL E
		ENDM
		LD HL,B_1WINA0
		LD A,4
ENS03		EX AF,AF'
		LD A,(HL)
		AND 0XC0
		CP 0XC0
		SET 3,B
		JR Z,ENS01
		RES 3,B
		LD A,(HL)
		OUT (C),A
		JR ENS02

ENS01		LD A,(HL)
		RLCA
		RLCA
		SRL D
		RRA
		SRL E
		RRA
		OUT (C),A
ENS02		LD A,B
		ADD A,0X40
		LD B,A
		DEC HL
		EX AF,AF'
		DEC A
		JR NZ,ENS03
		LD HL,(B_BF)
		LD BC,0XBC77
		LD A,H
		AND 0X0F
		OR 0XA0
		LD E,A
		LD A,H
		AND 0X80
		RRCA
		OR B
		LD B,A
		LD A,H
		AND 0X60
		RLCA
		RLCA
		RLCA
		OR B
		LD B,A
		OUT (C),E
		LD A,L
		OUT (PEVO_CONF),A
		LD BC,0X7FFD
		LD A,(B_7FFD)
		OUT (C),A
		LD A,0XED
		LD (FOR_RET),A
		XOR A
		LD (ENMISERVSYNC),A
		LD SP,0
RET_SP		EQU $-2
;HL AF IF RF DE BC HL' DE' BC' IX IY AF' 
		POP AF
		EX AF,AF'
		POP IY
		POP IX
		POP BC
		POP DE
		POP HL
		EXX
		POP BC
		POP DE
		POP AF
		LD R,A
		POP AF
		LD I,A
;		JP PO,ENMISERV
;		EI
;		LD A,0X76
;		LD (ENMISERVSYNC),A
ENMISERV	POP BC
		POP AF
		POP HL
		LD SP,(NMI_SAVE_SP)
ENMISERVSYNC	NOP
		OUT (0XBE),A
		RETN

		DEPHASE
ENMI_SERVICE
