
;LAST UPDATE: 28.09.2011 savelij

DD		EQU 9				;„€’€
MM		EQU 10				;Œ…‘Ÿ–
YY		EQU 11				;ƒŽ„
DATA		EQU DD+(MM<<5)+(YY<<9)+0X8000	;“†… “€ŠŽ‚€Ž

KOL_MODES	EQU 3
ADRNMIRST8	EQU 0X0013
DEPKADR		EQU 0X6000

		ORG 0X0000
		DI
		LD SP,DEPKADR
		LD IY,0X5C3A
		LD A,0X3F
		LD I,A
		IM 1
		XOR A
		OUT (0XFE),A
		JR RSTRAM0

		DUPL 0X0013-$,0XFF
		OUT (C),A
		NOP
		RET

RSTRAM0		LD HL,PAGES_CONF
		LD A,0XEF
		IN A,(0XFE)
		RRCA
		JR NC,RSTRAM
		LD BC,CMOSD_SET_ADR
		LD A,0XEF
		OUT (C),A
		LD B,HIGH (CMOSD_RD_WR)
		IN A,(C)
		AND 3
		CP KOL_MODES
		JR NC,RSTRAM
		AND A
		JR Z,RSTRAM
		JR RSTRAM1

		DUPL 0X0038-$,0XFF
		ORG 0X0038
		EI
		RET
		
RSTRAM1		DEC A
		LD HL,PAGES_CONFGLUK
		JR Z,RSTRAM
		LD HL,PAGES_PROFROM
RSTRAM		LD A,0XEF
		IN A,(0XFE)
		RRCA
		JR NC,RSTRAM
		LD BC,0XBC77
		LD A,2
		OUT (C),A
		LD DE,0X103F
LOOP2		LD BC,0X7FFD
		OUT (C),D
		LD BC,0XF7
LOOP1		OUTI
		LD A,B
		SUB E
		LD B,A
		JR NZ,LOOP1
		LD BC,0X7FFD
		LD A,D
		XOR 0X10
		LD D,A
		JR Z,LOOP2
RSTPAL		LD A,(HL)
		LD IXH,A			;ŽŒ… ‘’€ˆ–› ‡€‚…˜…ˆŸ ˆˆ’€ Œ€€ƒ…€ 
		LD A,%10100011			;ZX SCREEN MODE, TURBO OFF („‹Ÿ €’Œ)
		LD BC,0XFD77			;ˆ „Ž‘’“ Š €‹ˆ’…
		OUT (C),A
;¯ «¨âà 
		EI
		HALT
		DI
		LD HL,TRSTPAL			;+15
		LD DE,0XA30F
		LD BC,0XBD77
		OUT (C),D			;¢ª«.PAL
RSTPAL0		LD A,E
		BIT 3,E
		OUT (0XFE),A
		JR Z,$+4
		OUT (0XF6),A
		LD A,(HL)
		INC HL
		OR %00001100
		OUT (0XFF),A
		DEC E
		JP P,RSTPAL0
END_INIT	LD A,%10100011
		LD BC,0XFF77
		OUT (C),A			;¢ëª«.PAL
;~¯ «¨âà 
RSTKEYS		LD A,IXH
		LD BC,0
		CP 0X84
		JR C,RSTKEYS2
		CP 0X9C
		JR NC,RSTKEYS3
		JP LDIR4PROFROM

RSTKEYS3	PUSH BC
		LD BC,0XBC77
		LD A,2
		OUT (C),A
		LD BC,WIN_A0
		LD A,IXH
		OUT (C),A
		XOR A
		OUT (PEVO_CONF),A
		LD BC,0XFF77
		LD A,0XA3
		JP 0X3D2D

RSTKEYS2	LD BC,CMOSD_SET_ADR
		LD A,0XEF
		OUT (C),A
		LD B,HIGH (CMOSD_RD_WR)
		IN A,(C)
		AND 0XFC
		OUT (C),A
		LD BC,WIN_P2
		LD A,1
		OUT (C),A			
		LD BC,WIN_A3
		LD A,6
		OUT (C),A
		LD HL,0XC000
		LD DE,0X8000
		LD BC,0X4000
		LDIR				;……Ž‘ DOS ‚ …„Ž‘‹…„žž ‘’€ˆ–“
		LD BC,WIN_P2
		XOR A
		OUT (C),A
		LD HL,RAM_SEL
		LD DE,0X8000+ADRNMIRST8
		LD BC,ERAM_SEL-RAM_SEL
		LDIR				;“‘’€Ž‚Š€ Ž€Ž’—ˆŠ€
		LD HL,ERAM_SEL
		LD DE,0X8101
		LD BC,ENMI_SERVICE-ERAM_SEL
		LDIR				;“‘’€Ž‚Š€ Ž€Ž’—ˆŠ€
;		LD HL,0X9C33
;		XOR A
;		LD (HL),0XFF
;		INC HL
;		LD (HL),A
;		INC HL
;		LD (HL),A
;		INC HL
;		LD (HL),A
;		INC HL
;		LD (HL),0XFF
;		INC HL
;		LD (HL),A
;		INC HL
;		LD (HL),A
;		INC HL
;		LD (HL),A
;		INC HL
;		LD (HL),A
;		INC HL
;		LD (HL),0X23
;		INC HL
;		LD (HL),2
		LD BC,WIN_A3
		LD A,0XFF
		OUT (C),A
		LD BC,WIN_A2
		LD A,5
		OUT (C),A
		LD HL,0XB7F8
		LD DE,0XF800
		LD BC,0X0800
		LDIR				;“‘’€Ž‚Š€ ˜ˆ”’€
		LD BC,WIN_A2
		LD A,0X7D
		OUT (C),A
;		LD BC,WIN_P2
;		LD A,0XFD
;		OUT (C),A
		LD BC,0XFEFF
RSTKEYS1	LD A,B
		IN A,(0XFE)
		REPT 5
		RRA
		JR C,$+3
		INC C
		ENDM
		RLC B
		JR C,RSTKEYS1
		DEC C
		JP P,GTSTKEY			;€†€’Ž Ž‹…… 1 ŠŽŠˆ, ……•Ž„ˆŒ Š ’…‘’“ Š‹€‚ˆ€’“›
		LD DE,0				;1 €†€’€, Ž‚…Ÿ…Œ —’Ž ’Ž
		LD BC,0X7FFD
		PUSH DE				;€„…‘ ……•Ž„€ ‚ ‡“ …‘‹ˆ ‚›€Ž
		LD A,0XFE
		IN A,(0XFE)			;—’…ˆ… €†€’Ž‘’ˆ ŠŽŠˆ "CS"
		RRA
		LD A,0
		JP NC,START_SELECT		;ˆ €†€’Ž‰ "CS" ……•Ž„ˆŒ ‚ …‰‘ˆŠ 128
		LD A,0X7F
		IN A,(0XFE)			;—’…ˆ… €†€’Ž‘’ˆ ŠŽŠˆ "SS"
		LD D,A
		RRA
		RRA
		LD A,0X30
		JP NC,START_SELECT		;ˆ €†€’Ž‰ "SS" ……•Ž„ˆŒ ‚ …‰‘ˆŠ 48
		LD A,D
		RRA				;Ž‚…Š€ €†€’Ž‘’ˆ ŠŽŠˆ "SPACE"
		JR C,CMPCFG1			;……•Ž„ˆŒ „€‹…… …‘‹ˆ ˆ—…ƒŽ … €†€’Ž
		LD A,0X10
		LD DE,0X3D2F
		PUSH DE
		JP START_SELECT			;ˆ €†€’Ž‰ "SPACE" ……•Ž„ˆŒ ‚ TR-DOS

CMPCFG1		LD A,0XFD
		IN A,(0XFE)
		AND 4				;€†€’Ž "D" ‡€“‘Š „…ŒŠˆ
		JP Z,GDEMO
		LD HL,DEPKADR
		LD SP,HL
		EX DE,HL
		LD HL,LOADADR			;€‘€ŠŽ‚Š€ … ƒ‹žŠ€
		CALL DEC40
		XOR A
		IN A,(0XFE)
		CPL
		AND 0X1F
		LD A,0X10
		JR NZ,CMOSHELP
		LD HL,0X6000
		PUSH HL
		JR START_SELECT

GTSTKEY		SCF
		LD A,0X10
		
CMOSHELP	LD HL,BONUADR			;CMOS setup & HELP
		LD DE,0X6000			;€‘€ŠŽ‚Š€ ˆ ‡€“‘Š CMOS SETUP
		PUSH DE
		PUSH AF
		CALL DEC40
		POP AF
		JR START_SELECT

GDEMO		LD HL,0X6000-szdemoini
		LD SP,HL
		EX DE,HL
		PUSH DE
		LD HL,DEMO
		LD B,3
		LDIR
START_SELECT	EX AF,AF'			;‘Ž•€…ˆ… € ˆ ”‹€ƒŽ‚
		LD HL,ERAM_CODE-1
		LD DE,0X5C80
		LD BC,ERAM_CODE-RAM_CODE
		LDDR
		EX DE,HL
		INC HL
		EXX
		LD A,0X0E
		LD BC,CMOSD_SET_ADR
		OUT (C),A
		LD B,HIGH (CMOSD_RD_WR)
		IN E,(C)			;—’…ˆ… Ÿ—…‰Šˆ CMOS ‘ …†ˆŒ€Œˆ €ŒŸ’ˆ ˆ ’“›
		LD A,E
		AND 0X80
		RRCA
		RRCA
		RRCA
		LD D,A				;‘Ž•€ˆ‹ˆ ˆ’ ’“Ž …†ˆŒ€
		EXX
CMPCFG2		JP (HL)

RAM_CODE	LD BC,0X7FFD
		XOR A
		OUT (C),A
		INC A
		OUT (0XBF),A
		LD BC,WIN_A0
		LD A,IXH			;‘’€ˆ–€ …‰‘ˆŠ128
		OUT (C),A
		LD BC,0XFF77
		LD A,0XA3
		OUT (C),A
		XOR A
		OUT (0XBF),A
NOATM_CODE	EXX
		LD BC,PENT_CONF
		LD A,E
		AND 3				;‚‡Ÿ‹ˆ ˆ ‚›„…‹ˆ‹ˆ …†ˆŒ› €ŒŸ’ˆ
		JR Z,SET_MODE1
		SET 2,D				;„‹Ÿ …†ˆŒŽ‚ 48 ˆ 128 €‡…˜€…Œ ‹ŽŠˆŽ‚€ˆ… ‚…•…‰ €ŒŸ’ˆ
SET_MODE1	OUT (C),D
		EXX
		EX AF,AF'
		LD BC,0X7FFD
		OUT (C),A			;“‘’€Ž‚Š€ …†ˆŒ€ €ŒŸ’ˆ
		RET
ERAM_CODE

BONUADR		binclude cmosset_pack.rom
LOADADR		binclude main_pack.rom
		include nmi_service.a80

	       	DUPL 0X3D2D-$,0XFF
		OUT (C),A
		NOP
		RET

DEMO		LD BC,0X7FFD
		LD A,0X10
		OUT (C),A
		EI
szdemoini	EQU $-DEMO

		binclude grass.bin

		include dec40.a80

LDIR4PROFROM	LD HL,JMP2PROFROM
		LD DE,JMP2PROFROM+0X8000
		LD BC,EJMP2PROFROM-JMP2PROFROM
		LDIR
		PUSH BC
		LD BC,0XBC77
		LD A,3
		OUT (C),A
		LD A,IXH
		LD BC,WIN_A0
		OUT (C),A
		LD BC,0XFF77
		LD A,0XA3
		JP JMP2PROFROM+0X8000

JMP2PROFROM	OUT (C),A
		RET
EJMP2PROFROM	

;1000 0001 81 0 BASIC 48
;1000 0011 83 0 BASIC 128

;1000 0101 85 1
;1000 0111 87 1

;1000 1001 89 2
;1000 1110 8B 2

;1000 1101 8D 3
;1000 1111 8F 3

;1001 0001 91 4
;1001 0011 93 4

;1001 0101 95 5
;1001 0111 97 5

;1001 1001 99 6 BASIC 48
;1001 1011 9B 6 BASIC 128

;1001 1101 9D 7 BASIC 48
;1001 1111 9F 7 BASIC 128

PAGES_CONF	DB 0XFF,0X7D,0X7A,0X83
		DB 0XFF,0X7D,0X7A,0X00,0X81

PAGES_CONFGLUK	DB 0XFF,0X7D,0X7A,0X9F
		DB 0XFF,0X7D,0X7A,0X00,0X9D

PAGES_PROFROM	DB 0XFF,0X7D,0X7A,0X9B
		DB 0XFF,0X7D,0X7A,0X00,0X99

TRSTPAL		DB 0X00,0X21,0X42,0X63,0X90,0XB1,0XD2,0XF3
		DB 0XE0,0XE1,0XE2,0XE3,0XF0,0XF1,0XF2,0XF3

		DUPL 0X3FF8-$,0XFF
		DB "HEGLUK"
		DW DATA
